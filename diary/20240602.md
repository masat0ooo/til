### fishのfzf functionをここに退避

```
# プロンプトの再描画関数
function redraw_prompt
    commandline -f repaint
end

# Ctrl+Tでファイル選択
function fzf_select
    set file (fd --type f --hidden --exclude .git | fzf --preview 'bat --style=numbers --color=always --line-range=:500 {}' --height 40% --layout=reverse --border)
    if test -n "$file"
        commandline -i "$file"
    end
    redraw_prompt
end
bind \ct 'fzf_select'

# Ctrl+Rで履歴検索
function fzf_history
    set command (history | fzf --height 40% --layout=reverse --border --preview 'echo {}' --preview-window=up:3)
    if test -n "$command"
        commandline -i "$command"
    end
    redraw_prompt
end
bind \cr 'fzf_history'

# Ctrl+Gでディレクトリ検索
function fzf_cd
    set dir (fd --type d --hidden --exclude .git | fzf --height 40% --layout=reverse --border)
    if test -n "$dir"
        cd "$dir"
    end
    redraw_prompt
end
bind \cg 'fzf_cd'
```

父と妹は分かり合えないらしい。後になってお互いのありがらさに気がつくのに。